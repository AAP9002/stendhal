plugins {
	id 'com.android.application'
}

android {
	compileSdk 32

	def buildDate = new Date().format("yyyyMMdd")

	def appVer
	if (project.hasProperty("android.ver") && project["android.ver"]) {
		appVer = project["android.ver"]
	} else {
		appVer = "${buildDate}"
	}

	if (project.hasProperty("android.rev") && project["android.rev"]) {
		appVer += "-" + project["android.rev"]
	}

	def outputName = "stendhal-webview-${appVer}"

	defaultConfig {
		applicationId "org.stendhalgame.client"
		minSdk 21
		targetSdk 32
		versionCode 1
		versionName "${appVer}"
		setProperty("archivesBaseName", "${outputName}")
		setProperty("buildDir", "${projectDir}/../../../build/build_android_client")

		testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
		targetSdkVersion 32
		minSdkVersion 21
	}

	/* keystore.properties file must include the following values:
	 *   storeFile=<path-to-keystore-file>
	 *   storePassword=<store-password>
	 *   keyPassword=<key-password>
	 *   keyAlias=<key-alias>
	 */
	def keystorePropertiesFile = rootProject.file("keystore.properties")
	def keystoreProperties = new Properties()
	if (keystorePropertiesFile.exists()) {
		keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
	}

	signingConfigs {
		release {
			if (keystorePropertiesFile.exists()) {
				println "Using release signing key"
				storeFile file(keystoreProperties["storeFile"])
				storePassword keystoreProperties["storePassword"]
				keyPassword keystoreProperties["keyPassword"]
				keyAlias keystoreProperties["keyAlias"]
			} else {
				// default to debug key
				println "Using debug signing key"
				storeFile signingConfigs.debug.storeFile
				storePassword signingConfigs.debug.storePassword
				keyPassword signingConfigs.debug.keyPassword
				keyAlias signingConfigs.debug.keyAlias
			}
		}
	}

	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
			signingConfig signingConfigs.release
		}

		debug {
			applicationIdSuffix ".debug"
			versionNameSuffix "-debug"
			signingConfig signingConfigs.release
		}
	}

	buildTypes.all { type ->
		if (!type.name.equals("debug")) {
			sourceSets[type.name].java { srcDirs "src/shared/java/" }
		}
	}

	applicationVariants.all { variant ->
		variant.outputs.all {
			def buildRoot = "../../../../"
			if (variant.name.equals("release")) {
				// exclude "-release" suffix
				outputFileName = new File("${buildRoot}", "${outputName}.apk")
			} else {
				outputFileName = new File("${buildRoot}", "${outputFileName}")
			}
			println "Exporting apk to '${outputFileName}'"
		}
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}
	compileSdkVersion 32
	buildToolsVersion '32.0.0'

	lintOptions {
		abortOnError false
	}
}

dependencies {
	implementation 'androidx.appcompat:appcompat:1.4.1'
	implementation 'com.google.android.material:material:1.4.0'
	//testImplementation 'junit:junit:4.+'
	//androidTestImplementation 'androidx.test.ext:junit:1.1.2'
	//androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}
