// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
	repositories {
		google()
		mavenCentral()
	}
	dependencies {
		classpath "com.android.tools.build:gradle:7.1.1"

		// NOTE: Do not place your application dependencies here; they belong
		// in the individual module build.gradle files
	}
}

task clean(type: Delete) {
	delete rootProject.buildDir
	delete rootProject.file("deps")
}

task buildDeps {
	doLast {
		def winOS = System.properties["os.name"].toLowerCase(Locale.ENGLISH).startsWith("windows")

		println "building Marauroa ..."

		// check for Marauroa source
		def marauroaDir = rootProject.file("deps/marauroa")
		if (!marauroaDir.exists()) {
			marauroaDir.mkdirs()
		}
		if (!file(marauroaDir.toString() + "/build.xml").exists()) {
			// download Marauroa source
			exec {
				workingDir marauroaDir
				commandLine "git", "clone", "--single-branch", "--branch=master", "https://github.com/arianne/marauroa.git", "./"
			}
		}

		def marauroaJar = file(marauroaDir.toString() + "/build-archive/marauroa.jar")
		if (!marauroaJar.exists()) {
			def antCmd = "ant"
			if (winOS) {
				antCmd += ".bat"
			}

			exec {
				workingDir marauroaDir
				commandLine antCmd, "jar"
			}
		}
	}
}

allprojects {
	tasks.withType(JavaCompile) {
		options.deprecation = true
	}
}
