plugins {
	id 'com.android.application'
}

android {
	compileSdk 32

	def buildDate = new Date().format("yyyyMMdd")
	if (project.hasProperty("android.rev") && project["android.rev"]) {
		buildDate += "-" + project["android.rev"]
	}

	def outputName = "stendhal-webview-${buildDate}"

	defaultConfig {
		applicationId "org.arianne.stendhal.client"
		minSdk 21
		targetSdk 32
		versionCode 1
		versionName "${buildDate}"
		setProperty("archivesBaseName", "${outputName}")
		setProperty("buildDir", "${projectDir}/../../build/build_android_client")

		testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
		targetSdkVersion 32
		minSdkVersion 21
	}

	applicationVariants.all { variant ->
		if (variant.buildType.name == "debug") {
			variant.outputs.all { output ->
				output.versionNameOverride = buildDate + "-debug"
			}
		} else if (variant.buildType.name == "prerelease") {
			variant.outputs.all { output ->
				output.versionNameOverride = buildDate + "-pre"
			}
		}
	}

	signingConfigs {
		androiddebug {
			// set this to the location of android debug.keystore file (usually ~/.android/debug.keystore)
			storeFile file(System.getenv("HOME") + "/.android/debug.keystore")
			keyAlias "androiddebugkey"
			storePassword "android"
			keyPassword "android"
		}
	}

	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
		}

		prerelease {
			signingConfig signingConfigs.androiddebug
		}
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}
	compileSdkVersion 32
	buildToolsVersion '32.0.0'

	lintOptions {
		abortOnError false
	}
}

dependencies {

	implementation 'androidx.appcompat:appcompat:1.4.1'
	implementation 'com.google.android.material:material:1.4.0'
	testImplementation 'junit:junit:4.+'
	androidTestImplementation 'androidx.test.ext:junit:1.1.2'
	androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}
